// SPDX-License-Identifier: LicenseRef-Degensoft-SwapVM-1.1
pragma solidity 0.8.30;

/// @custom:license-url https://github.com/1inch/swap-vm/blob/main/LICENSES/SwapVM-1.1.txt
/// @custom:copyright © 2025 Degensoft Ltd

import { EIP712 } from "@openzeppelin/contracts/utils/cryptography/EIP712.sol";
import { ECDSA } from "@1inch/solidity-utils/contracts/libraries/ECDSA.sol";
import { SafeERC20, IERC20, IWETH } from "@1inch/solidity-utils/contracts/libraries/SafeERC20.sol";
import { IAqua } from "@1inch/aqua/src/interfaces/IAqua.sol";

import { TransientLock } from "@1inch/solidity-utils/contracts/libraries/TransientLock.sol";
import { TransientLockUnsafeLib } from "@1inch/solidity-utils/contracts/libraries/TransientLockUnsafe.sol";
import { CalldataPtrLib } from "@1inch/solidity-utils/contracts/libraries/CalldataPtr.sol";
import { OnlyWethReceiver } from "@1inch/solidity-utils/contracts/mixins/OnlyWethReceiver.sol";
import { Rescuable } from "@1inch/solidity-utils/contracts/mixins/Rescuable.sol";

import { ISwapVM } from "./interfaces/ISwapVM.sol";
import { IMakerHooks } from "./interfaces/IMakerHooks.sol";
import { ITakerCallbacks } from "./interfaces/ITakerCallbacks.sol";
import { Context, ContextLib, VM, SwapRegisters, SwapQuery, ProtocolFee } from "./libs/VM.sol";
import { MakerTraits, MakerTraitsLib } from "./libs/MakerTraits.sol";
import { TakerTraits, TakerTraitsLib } from "./libs/TakerTraits.sol";
import { FeeMetaLib, FeeReceiverLib } from "./libs/ProtocolFee.sol";

/// @title SwapVM
/// @notice Virtual machine for executing programmable token swap strategies from bytecode
/// @dev Abstract contract that must be inherited by routers defining instruction sets
/// @dev This contract is Ownable via Rescuable mixin
abstract contract SwapVM is EIP712, OnlyWethReceiver, Rescuable {
    using ECDSA for address;
    using SafeERC20 for IERC20;
    using SafeERC20 for IWETH;
    using TransientLockUnsafeLib for TransientLock;
    using ContextLib for Context;
    using MakerTraitsLib for MakerTraits;
    using TakerTraitsLib for TakerTraits;

    /// @dev Signature verification failed for the order
    error BadSignature(address maker, bytes32 orderHash, bytes signature);
    /// @dev Aqua balance insufficient after taker pushed tokens
    error AquaBalanceInsufficientAfterTakerPush(uint256 balance, uint256 preBalance, uint256 amount);
    /// @dev Cannot use shouldUnwrapWeth with Aqua orders
    error MakerTraitsUnwrapIsIncompatibleWithAqua();
    /// @dev Cannot use custom receiver with Aqua orders
    error MakerTraitsCustomReceiverIsIncompatibleWithAqua();
    /// @dev Cannot pay with native coin for other token than WETH
    error MsgValueInvalidToken();
    /// @dev Attached native coin does not cover amountIn fully
    error NotEnoughMsgValueAttached();
    /// @dev Payment in native coin is unexpected
    error UnexpectedMsgValue();
    /// @dev Native coin transfer failed
    error EthTransferFailed();

    /// @notice Emitted when a swap is successfully executed
    /// @param orderHash Unique identifier for the order
    /// @param maker Address of the liquidity provider
    /// @param taker Address that executed the swap
    /// @param tokenIn Input token address
    /// @param tokenOut Output token address
    /// @param amountIn Input token amount
    /// @param amountOut Output token amount
    event Swapped(
        bytes32 orderHash,
        address maker,
        address taker,
        address tokenIn,
        address tokenOut,
        uint256 amountIn,
        uint256 amountOut
    );

    /// @notice EIP-712 typehash for Order struct
    bytes32 public constant ORDER_TYPEHASH = keccak256(
        "Order("
            "address maker,"
            "uint256 traits,"
            "bytes data"
        ")"
    );

    /// @notice Aqua protocol instance for balance management
    IAqua public immutable AQUA;
    IWETH public immutable WETH;

    mapping(bytes32 orderHash => TransientLock) private _reentrancyGuards;

    /// @notice Initialize SwapVM with Aqua and WETH addresses
    /// @param aqua Address of the Aqua protocol contract
    /// @param weth Address of the WETH token
    /// @param owner Address of the owner of the contract, used for rescuing funds only
    /// @param name EIP-712 domain name
    /// @param version EIP-712 domain version
    constructor(address aqua, address weth, address owner, string memory name, string memory version) EIP712(name, version) OnlyWethReceiver(weth) Rescuable(owner) {
        AQUA = IAqua(aqua);
        WETH = IWETH(weth);
    }

    /// @notice Cast contract to ISwapVM interface for view-only operations
    /// @return Interface instance for static calls
    function asView() external view returns (ISwapVM) {
        return ISwapVM(address(this));
    }

    /// @notice Compute unique hash for an order
    /// @param order The maker's order structure
    /// @return Unique identifier for this order/strategy
    function hash(ISwapVM.Order calldata order) public view returns (bytes32) {
        if (order.traits.useAquaInsteadOfSignature()) {
            return keccak256(abi.encode(order));
        }

        return _hashTypedDataV4(keccak256(abi.encode(
            ORDER_TYPEHASH,
            order.maker,
            order.traits,
            keccak256(order.data)
        )));
    }

    /// @dev Method can be executed in a static-call
    function quote(
        ISwapVM.Order calldata order,
        uint256 amount,
        bytes calldata takerTraitsAndData
    ) external returns (uint256 amountIn, uint256 amountOut, bytes32 orderHash) {
        orderHash = hash(order);

        (TakerTraits takerTraits, bytes calldata takerData) = TakerTraitsLib.parse(takerTraitsAndData);
        bool isExactIn = takerTraits.isExactIn();

        address tokenIn;
        address tokenOut;
        if (takerTraits.isAToB()) (tokenIn, tokenOut) = order.traits.tokens(order.data);
        else (tokenOut, tokenIn) = order.traits.tokens(order.data);

        Context memory ctx = Context({
            vm: VM({
                isStaticContext: true,
                nextPC: 0,
                programPtr: CalldataPtrLib.from(order.traits.program(order.data)),
                takerArgsPtr: CalldataPtrLib.from(takerTraits.instructionsArgs(takerData)),
                dispatch: _dispatch
            }),
            query: SwapQuery({
                orderHash: orderHash,
                maker: order.maker,
                taker: msg.sender,
                tokenIn: tokenIn,
                tokenOut: tokenOut,
                isExactIn: isExactIn
            }),
            swap: SwapRegisters({
                balanceIn: 0,
                balanceOut: 0,
                amountIn: isExactIn ? amount : 0,
                amountOut: isExactIn ? 0 : amount
            }),
            fee: ProtocolFee({
                meta: FeeMetaLib.init(),
                receivers: FeeReceiverLib.init(),
                feeTotal: 0
            })
        });

        if (order.traits.useAquaInsteadOfSignature()) {
            (ctx.swap.balanceIn, ctx.swap.balanceOut) = AQUA.safeBalances(order.maker, address(this), orderHash, tokenIn, tokenOut);
        }

        (amountIn, amountOut) = ctx.runLoop();
        order.traits.validate(amountIn);
        takerTraits.validate(takerData, amount, amountIn, amountOut);
    }

    function swap(
        ISwapVM.Order calldata order,
        uint256 amount,
        bytes calldata takerTraitsAndData
    ) external payable returns (uint256 amountIn, uint256 amountOut, bytes32 orderHash) {
        orderHash = hash(order);
        _reentrancyGuards[orderHash].lock();

        (TakerTraits takerTraits, bytes calldata takerData) = TakerTraitsLib.parse(takerTraitsAndData);
        bool isExactIn = takerTraits.isExactIn();

        address tokenIn;
        address tokenOut;
        if (takerTraits.isAToB()) (tokenIn, tokenOut) = order.traits.tokens(order.data);
        else (tokenOut, tokenIn) = order.traits.tokens(order.data);

        Context memory ctx = Context({
            vm: VM({
                isStaticContext: false,
                nextPC: 0,
                programPtr: CalldataPtrLib.from(order.traits.program(order.data)),
                takerArgsPtr: CalldataPtrLib.from(takerTraits.instructionsArgs(takerData)),
                dispatch: _dispatch
            }),
            query: SwapQuery({
                orderHash: orderHash,
                maker: order.maker,
                taker: msg.sender,
                tokenIn: tokenIn,
                tokenOut: tokenOut,
                isExactIn: isExactIn
            }),
            swap: SwapRegisters({
                balanceIn: 0,
                balanceOut: 0,
                amountIn: isExactIn ? amount : 0,
                amountOut: isExactIn ? 0 : amount
            }),
            fee: ProtocolFee({
                meta: FeeMetaLib.init(),
                receivers: FeeReceiverLib.init(),
                feeTotal: 0
            })
        });

        if (order.traits.useAquaInsteadOfSignature()) {
            (ctx.swap.balanceIn, ctx.swap.balanceOut) = AQUA.safeBalances(order.maker, address(this), orderHash, tokenIn, tokenOut);
        } else {
            bytes calldata signature = takerTraits.signature(takerData);
            require(order.maker.recoverOrIsValidSignature(orderHash, signature), BadSignature(order.maker, orderHash, signature));
        }

        uint256 originalAquaBalanceIn = ctx.swap.balanceIn;
        (amountIn, amountOut) = ctx.runLoop();
        order.traits.validate(amountIn);
        takerTraits.validate(takerData, amount, amountIn, amountOut);

        if (takerTraits.isFirstTransferFromTaker()) {
            _transferIn(ctx, order, takerTraits, takerData, originalAquaBalanceIn);
            _transferOut(ctx, order, takerTraits, takerData);
        } else {
            _transferOut(ctx, order, takerTraits, takerData);
            _transferIn(ctx, order, takerTraits, takerData, originalAquaBalanceIn);
        }

        _reentrancyGuards[orderHash].unlock();
        emit Swapped(orderHash, order.maker, msg.sender, tokenIn, tokenOut, amountIn, amountOut);
    }

    function _transferIn(Context memory ctx, ISwapVM.Order calldata order, TakerTraits takerTraits, bytes calldata takerData, uint256 originalAquaBalanceIn) private {
        if (order.traits.hasPreTransferInHook()) {
            (IMakerHooks target, bytes calldata makerHookData) = order.traits.preTransferInHook(order.maker, order.data);
            bytes calldata takerHookData = takerTraits.preTransferInHookData(takerData);
            target.preTransferIn(order.maker, ctx.query.taker, ctx.query.tokenIn, ctx.query.tokenOut, ctx.swap.amountIn, ctx.swap.amountOut, ctx.query.orderHash, makerHookData, takerHookData);
        }

        if (takerTraits.hasPreTransferInCallback()) {
            bytes calldata callbackData = takerTraits.preTransferInCallbackData(takerData);
            ITakerCallbacks(ctx.query.taker).preTransferInCallback(order.maker, ctx.query.taker, ctx.query.tokenIn, ctx.query.tokenOut, ctx.swap.amountIn, ctx.swap.amountOut, ctx.query.orderHash, callbackData);
        }

        uint256 fee;
        require(msg.value == 0 || ctx.query.tokenIn == address(WETH), MsgValueInvalidToken());
        if (ctx.swap.amountIn > 0) {
            if (order.traits.useAquaInsteadOfSignature()) {
                require(!order.traits.shouldUnwrapWeth(), MakerTraitsUnwrapIsIncompatibleWithAqua());
                require(order.maker == order.traits.receiver(order.maker), MakerTraitsCustomReceiverIsIncompatibleWithAqua());

                if (takerTraits.useTransferFromAndAquaPush()) {
                    if (_acceptNativePayment(ctx.swap.amountIn)) {
                        WETH.safeDeposit(ctx.swap.amountIn);
                    } else {
                        IERC20(ctx.query.tokenIn).safeTransferFrom(ctx.query.taker, address(this), ctx.swap.amountIn);
                    }
                    fee = FeeMetaLib.resolveInSafeTransfer(ctx.fee, ctx.query.tokenIn, ctx.swap.amountIn);

                    IERC20(ctx.query.tokenIn).forceApprove(address(AQUA), ctx.swap.amountIn - fee);
                    AQUA.push(order.maker, address(this), ctx.query.orderHash, ctx.query.tokenIn, ctx.swap.amountIn - fee);
                } else {
                    require(msg.value == 0, UnexpectedMsgValue());
                    (uint256 balanceIn,) = AQUA.rawBalances(order.maker, address(this), ctx.query.orderHash, ctx.query.tokenIn);
                    require(balanceIn >= originalAquaBalanceIn + ctx.swap.amountIn, AquaBalanceInsufficientAfterTakerPush(balanceIn, originalAquaBalanceIn, ctx.swap.amountIn));

                    fee = FeeMetaLib.resolveInAquaPullMaker(ctx.fee, ctx.query.tokenIn, ctx.swap.amountIn, AQUA, order.maker, ctx.query.orderHash);
                }
            } else if (_acceptNativePayment(ctx.swap.amountIn)) {
                WETH.safeDeposit(ctx.swap.amountIn);
                fee = FeeMetaLib.resolveInSafeTransfer(ctx.fee, ctx.query.tokenIn, ctx.swap.amountIn);

                if (order.traits.shouldUnwrapWeth()) {
                    WETH.safeWithdrawTo(ctx.swap.amountIn - fee, order.traits.receiver(order.maker));
                } else {
                    IERC20(ctx.query.tokenIn).safeTransfer(order.traits.receiver(order.maker), ctx.swap.amountIn - fee);
                }
            } else {
                fee = FeeMetaLib.resolveInSafeTransferFromTaker(ctx.fee, ctx.query.tokenIn, ctx.swap.amountIn, ctx.query.taker);
                _transferFrom(ctx.query.taker, order.traits.receiver(order.maker), ctx.query.tokenIn, ctx.swap.amountIn - fee, ctx.query.orderHash, false, order.traits.shouldUnwrapWeth());
            }
        } else {
            if (msg.value > 0) _sendEth(msg.sender, msg.value);
        }

        if (order.traits.hasPostTransferInHook()) {
            (IMakerHooks target, bytes calldata makerHookData) = order.traits.postTransferInHook(order.maker, order.data);
            bytes calldata takerHookData = takerTraits.postTransferInHookData(takerData);
            target.postTransferIn(order.maker, ctx.query.taker, ctx.query.tokenIn, ctx.query.tokenOut, ctx.swap.amountIn, ctx.swap.amountOut, fee, ctx.query.orderHash, makerHookData, takerHookData);
        }
    }

    function _acceptNativePayment(uint256 amount) internal returns (bool) {
        if (msg.value == 0) return false;

        require(msg.value >= amount, NotEnoughMsgValueAttached());

        uint256 remaining;
        unchecked {
            remaining = msg.value - amount;
        }

        if (remaining > 0) _sendEth(msg.sender, remaining);

        return true;
    }

    function _sendEth(address to, uint256 amount) private {
        (bool success, ) = to.call{ value: amount }("");
        require(success, EthTransferFailed());
    }

    function _transferOut(Context memory ctx, ISwapVM.Order calldata order, TakerTraits takerTraits, bytes calldata takerData) private {
        if (order.traits.hasPreTransferOutHook()) {
            (IMakerHooks target, bytes calldata makerHookData) = order.traits.preTransferOutHook(order.maker, order.data);
            bytes calldata takerHookData = takerTraits.preTransferOutHookData(takerData);
            target.preTransferOut(order.maker, ctx.query.taker, ctx.query.tokenIn, ctx.query.tokenOut, ctx.swap.amountIn, ctx.swap.amountOut, ctx.query.orderHash, makerHookData, takerHookData);
        }

        if (takerTraits.hasPreTransferOutCallback()) {
            bytes calldata callbackData = takerTraits.preTransferOutCallbackData(takerData);
            ITakerCallbacks(ctx.query.taker).preTransferOutCallback(order.maker, ctx.query.taker, ctx.query.tokenIn, ctx.query.tokenOut, ctx.swap.amountIn, ctx.swap.amountOut, ctx.query.orderHash, callbackData);
        }

        uint256 fee;
        if (order.traits.useAquaInsteadOfSignature()) fee = FeeMetaLib.resolveOutAquaPullMaker(ctx.fee, ctx.query.tokenOut, ctx.swap.amountOut, AQUA, order.maker, ctx.query.orderHash);
        else fee = FeeMetaLib.resolveOutSafeTransferFromMaker(ctx.fee, ctx.query.tokenOut, ctx.swap.amountOut, order.maker);

        _transferFrom(order.maker, takerTraits.to(takerData, msg.sender), ctx.query.tokenOut, ctx.swap.amountOut, ctx.query.orderHash, order.traits.useAquaInsteadOfSignature(), takerTraits.shouldUnwrapWeth());

        if (order.traits.hasPostTransferOutHook()) {
            (IMakerHooks target, bytes calldata makerHookData) = order.traits.postTransferOutHook(order.maker, order.data);
            bytes calldata takerHookData = takerTraits.postTransferOutHookData(takerData);
            target.postTransferOut(order.maker, ctx.query.taker, ctx.query.tokenIn, ctx.query.tokenOut, ctx.swap.amountIn, ctx.swap.amountOut, fee, ctx.query.orderHash, makerHookData, takerHookData);
        }
    }

    function _transferFrom(address from, address to, address token, uint256 amount, bytes32 orderHash, bool useAqua, bool unwrapWeth) private {
        if (unwrapWeth && token == address(WETH)) {
            _transferOrPull(from, address(this), token, amount, orderHash, useAqua);
            IWETH(token).safeWithdrawTo(amount, to);
        } else {
            _transferOrPull(from, to, token, amount, orderHash, useAqua);
        }
    }

    function _transferOrPull(address from, address to, address token, uint256 amount, bytes32 orderHash, bool useAqua) private {
        if (useAqua) {
            AQUA.pull(from, orderHash, token, amount, to);
        } else {
            IERC20(token).safeTransferFrom(from, to, amount);
        }
    }

    /// @dev Override in the opcode set to directly dispatch an opcode at specified index
    function _dispatch(Context memory ctx, uint256 opcode, bytes calldata args) internal virtual;
}
