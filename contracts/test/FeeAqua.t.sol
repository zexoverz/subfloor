// SPDX-License-Identifier: LicenseRef-Degensoft-SwapVM-1.1
pragma solidity 0.8.30;

/// @custom:license-url https://github.com/1inch/swap-vm/blob/main/LICENSES/SwapVM-1.1.txt
/// @custom:copyright © 2025 Degensoft Ltd

import { Math } from "@openzeppelin/contracts/utils/math/Math.sol";
import { AquaSwapVMTest } from "./base/AquaSwapVMTest.sol";

import { ISwapVM } from "../src/interfaces/ISwapVM.sol";
import { FeeFlatIn } from "../src/instructions/FeeFlat.sol";
import { ContextLib } from "../src/libs/VM.sol";
import { TakerTraitsLib } from "../src/libs/TakerTraits.sol";

contract FeeAquaTest is AquaSwapVMTest {
    function setUp() public virtual override {
        super.setUp();
    }

    function _makerSetup(
        uint24 feeInBps
    ) internal pure returns (MakerSetup memory) {
        return MakerSetup({
            balanceA: INITIAL_BALANCE_A,
            balanceB: INITIAL_BALANCE_B,
            priceMin: 0,
            priceMax: 0,
            protocolFeeBps: 0,
            feeInBps: feeInBps,
            protocolFeeRecipient: address(0),
            swapType: SwapType.XYC
        });
    }

    function _swapProgram(
        uint256 amount,
        bool zeroForOne,
        bool isExactIn
    ) internal view returns (SwapProgram memory) {
        return SwapProgram({
            amount: amount,
            taker: taker,
            tokenA: tokenA,
            tokenB: tokenB,
            zeroForOne: zeroForOne,
            isExactIn: isExactIn
        });
    }

    function test_Aqua_FeeIn_ExactIn_BalanceAfterSwap() public {
        MakerSetup memory setup = _makerSetup(0.10e7); // 10% fee in
        ISwapVM.Order memory order = createStrategy(setup);
        bytes32 strategyHash = shipStrategy(order, tokenA, tokenB, setup.balanceA, setup.balanceB);
        SwapProgram memory swapProgram = _swapProgram(100e18, true, true); // Swap 100 tokenA for tokenB

        (uint256 makerBalanceABefore, uint256 makerBalanceBBefore) = getAquaBalances(strategyHash);

        mintTokenInToTaker(swapProgram);
        (uint256 takerBalanceABefore, uint256 takerBalanceBBefore) = getTakerBalances(swapProgram.taker);

        mintTokenOutToMaker(swapProgram, 200e18);
        (uint256 amountIn, uint256 amountOut) = swap(swapProgram, order);

        (uint256 makerBalanceAAfter, uint256 makerBalanceBAfter) = getAquaBalances(strategyHash);
        (uint256 takerBalanceAAfter, uint256 takerBalanceBAfter) = getTakerBalances(swapProgram.taker);

        uint256 expectedFee = amountIn * setup.feeInBps / FeeFlatIn.BPS;
        uint256 amountOutExpected = setup.balanceB * (amountIn - expectedFee) / (setup.balanceA + amountIn - expectedFee);
        assertEq(takerBalanceBAfter - takerBalanceBBefore, amountOutExpected, "Taker received correct amountOut");
        assertEq(makerBalanceAAfter, makerBalanceABefore + amountIn, "Maker balance A should increase by amountIn");
        assertEq(makerBalanceBAfter, makerBalanceBBefore - amountOut, "Maker balance B should decrease by amountOut");
        assertEq(takerBalanceAAfter, takerBalanceABefore - amountIn, "Taker balance A should decrease by amountIn");
        assertEq(takerBalanceBAfter, takerBalanceBBefore + amountOut, "Taker balance B should increase by amountOut");
    }

    function test_Aqua_FeeIn_ExactOut_BalancesAfterSwap() public {
        MakerSetup memory setup = _makerSetup(0.10e7); // 10% fee in
        ISwapVM.Order memory order = createStrategy(setup);
        bytes32 strategyHash = shipStrategy(order, tokenA, tokenB, setup.balanceA, setup.balanceB);
        SwapProgram memory swapProgram = _swapProgram(100e18, true, false); // Swap for 100 tokenB

        (uint256 makerBalanceABefore, uint256 makerBalanceBBefore) = getAquaBalances(strategyHash);

        mintTokenInToTaker(swapProgram);
        (uint256 takerBalanceABefore, uint256 takerBalanceBBefore) = getTakerBalances(swapProgram.taker);

        mintTokenOutToMaker(swapProgram, 200e18);
        (uint256 amountIn, uint256 amountOut) = swap(swapProgram, order);

        (uint256 makerBalanceAAfter, uint256 makerBalanceBAfter) = getAquaBalances(strategyHash);
        (uint256 takerBalanceAAfter, uint256 takerBalanceBAfter) = getTakerBalances(swapProgram.taker);

        uint256 baseAmountIn = Math.ceilDiv(setup.balanceA * amountOut, setup.balanceB - amountOut);
        uint256 expectedFee = Math.ceilDiv(baseAmountIn * uint256(setup.feeInBps), FeeFlatIn.BPS - uint256(setup.feeInBps));
        uint256 amountInExpected = baseAmountIn + expectedFee;
        assertApproxEqAbs(takerBalanceABefore - takerBalanceAAfter, amountInExpected, 1, "Taker paid correct amountIn");
        assertEq(makerBalanceAAfter, makerBalanceABefore + amountIn, "Maker balance A should increase by amountIn");
        assertEq(makerBalanceBAfter, makerBalanceBBefore - amountOut, "Maker balance B should decrease by amountOut");
        assertEq(takerBalanceAAfter, takerBalanceABefore - amountIn, "Taker balance A should decrease by amountIn");
        assertEq(takerBalanceBAfter, takerBalanceBBefore + amountOut, "Taker balance B should increase by amountOut");
    }

    function test_Aqua_FeeIn_ExactIn_100Percent_ShouldRevert() public {
        MakerSetup memory setup = _makerSetup(uint24(FeeFlatIn.BPS - 1)); // ~100% fee in (BPS-1, exactly 100% is rejected by build)
        ISwapVM.Order memory order = createStrategy(setup);
        shipStrategy(order, tokenA, tokenB, setup.balanceA, setup.balanceB);
        // Amount below BPS so the ~100% fee rounds the net input down to zero
        SwapProgram memory swapProgram = _swapProgram(1e6, true, true);

        mintTokenInToTaker(swapProgram);
        mintTokenOutToMaker(swapProgram, 200e18);

        vm.expectRevert(abi.encodeWithSelector(TakerTraitsLib.TakerTraitsAmountOutMustBeGreaterThanZero.selector, 0));
        swap(swapProgram, order);
    }

    function test_Aqua_FeeIn_ExactOut_100Percent_ShouldRevert() public {
        MakerSetup memory setup = _makerSetup(uint24(FeeFlatIn.BPS - 1)); // ~100% fee
        ISwapVM.Order memory order = createStrategy(setup);
        shipStrategy(order, tokenA, tokenB, setup.balanceA, setup.balanceB);
        SwapProgram memory swapProgram = _swapProgram(100e18, true, false); // Swap for 100 tokenB

        mintTokenInToTaker(swapProgram);
        mintTokenOutToMaker(swapProgram, 200e18);

        // impossible to pay 100% feeIn on exactOut swap
        vm.expectRevert();
        swap(swapProgram, order);
    }
}
